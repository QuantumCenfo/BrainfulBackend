plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'jacoco'
	id 'org.sonarqube' version '4.4.1.3373'
}

group = 'com.project'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    // https://mvnrepository.com/artifact/com.azure/azure-storage-blob
    implementation group: 'com.azure', name: 'azure-storage-blob', version: '12.23.0'
    implementation 'com.azure.spring:azure-spring-boot-starter-keyvault-secrets:3.14.0'

    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client:3.1.2' // Or the latest stable version

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // ðŸ”¹ Dependencias agregadas para pruebas REST Assured
    testImplementation 'io.rest-assured:rest-assured:5.4.0'
    testImplementation 'io.rest-assured:json-path:5.4.0'
    testImplementation 'io.rest-assured:json-schema-validator:5.4.0'

    // ðŸ”¹ JUnit 5 (para que use la versiÃ³n mÃ¡s reciente del motor Jupiter)
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'

    // ðŸ”¹ Testcontainers (para pruebas de integraciÃ³n con bases de datos)
    testImplementation platform('org.testcontainers:testcontainers-bom:1.20.3')
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:mariadb'
}

bootJar {
    archiveBaseName.set('demo-java-spring-api')
    archiveVersion.set('1.0.0')
    archiveFileName.set('demo-java-spring-api-1.0.0.jar')
    launchScript()
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy tasks.jacocoTestReport

    if (System.getenv("CI") == "true") {
        ignoreFailures = true
    }
}

jacoco {
	toolVersion = '0.8.11'
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
}

sonarqube {
  properties {
    property "sonar.projectKey", "brainfullapp"
    property "sonar.organization", "BrainfullApp"
    property "sonar.sourceEncoding", "UTF-8"

    property "sonar.sources", "src/main/java"
    property "sonar.tests",   "src/test/java"
    property "sonar.java.binaries", "build/classes/java/main"

    property "sonar.junit.reportPaths", "build/test-results/test"
    property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
  }
}
